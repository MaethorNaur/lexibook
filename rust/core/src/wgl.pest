wgl = _{ SOI ~ NEWLINE* ~ 
  (import ~ NEWLINE+)? ~ 
  (
   (letters |
    phonemes |
    class |
    syllables |
    rules
   )
    ~ NEWLINE+)* ~ 
  EOI
}

import = { ^"import" ~ ":" ~ file ~ file* }

file = @{ (ASCII_ALPHA_UPPER ~ ":\\" )? ~ relative_or_parent? ~ filename ~ (separator_file ~ relative_or_parent? ~ filename )*  }
relative_or_parent = @{"."{1,2} ~ separator_file}
separator_file = _{"\\" | "/"}

filename = @{ ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | "_" | "-" | ".")* }

letters = { ^"letters" ~ ":" ~ letter_distribution ~ ("," ~ NEWLINE* ~ letter_distribution)* }
letter_distribution = { letter ~ (":" ~ number)? }
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT*)?
}

letter = @{ (!(NEWLINE | WHITESPACE | ":" | "," | "\"") ~ ANY)+ }

syllables = { ^"syllables" ~ ":"  ~ words  ~ words* }
words = ${ word  ~ word* }
word = ${ UPPERCASE_LETTER ~ "?"? }

class = { class_name ~ "=" ~ letter+}
class_name = {UPPERCASE_LETTER}

phonetic_notation = {  
 ASCII_ALPHA_LOWER | 
  "ˈ" | "ˌ" | "ː" |"ˑ"| "\u{306}" |
  "|" | "‖" | "↗" | "↘" |
  "\u{30B}" | "˥" | "\u{301}" | "˦" | "\u{304}" | "˧" | "\u{300}" | "˨" | "\u{30F}" | "˩" | "\u{30C}" | "\u{302}" | "ꜛ" | "ꜜ" |
  "."| "ʙ" |"ʀ" |"ⱱ" | "ɾ" | "ɽ" | "ɕ" | "ʑ" | "ç" | "ʝ" | "ɣ" | "χ" | "ʁ" | "ħ" | "ʕ" | "ɦ" | "ɬ" | "ɮ" | "ʋ" | "ɰ" | "ɭ" | "ʎ" | "ʟ" | "ɺ" |
  "ɨ" | "ʉ" | "ɯ" | "ɪ" | "ʏ" | "ø" | "ɘ" | "ɵ" | "ɤ" | "ə" | "ɛ" | "œ" | "ɜ" | "ɞ" | "ʌ" | "ɔ" | "æ" | "ɐ" | "ɶ" | "ɑ" | "ɒ" |
  "ɱ" | "ɳ" | "ŋ" | "ɴ" | "ʈ" | "ɖ" | "ɟ" | "ɢ" |
  "ʡ" | "ʔ" | "ʃ" | "ʒ" | "ʂ" | "ʐ" | "ɸ" | "β" | "θ" | "ð" | "ɹ" | "ɻ" |
  "ˤ" | "ⁿ" | "ˡ" | "ᶿ" | "ˣ" | "ᵊ" | "ʱ" | "ʷ" | "ʲ" | "ˠ" | "ɚ" | "ʰ" | "\u{33D}" |
  "\u{32C}" | "\u{324}" | "\u{330}" | "\u{32A}" | "\u{33C}" | "\u{33A}" | "\u{33B}" | "\u{31F}" | "\u{2D6}" | "\u{320}" | "\u{2D7}" | "\u{308}" | "\u{31D}" | "\u{2D4}" |
  "\u{31E}" | "\u{2D5}" | "\u{329}" | "\u{339}" | "\u{31C}" | "\u{334}" | "\u{318}" | "\u{318}" | "\u{319}" | "\u{303}" | "\u{2DE}" | "\u{30A}" |  "\u{325}"
}

phonemes = { ^"phonemes" ~ ":" ~ NEWLINE* ~ phoneme ~ ( NEWLINE+ ~ phoneme)*}
phoneme = ${WHITESPACE* ~ letter+ ~  WHITESPACE+ ~ "/" ~ phonetic_notations ~ "/" ~ (WHITESPACE+ ~ phoneme_conditions ~ WHITESPACE*)?}
phoneme_conditions = { (NOT ~ WHITESPACE+)? ~ phoneme_condition ~ ( WHITESPACE+ ~ (AND | OR) ~ (WHITESPACE+ | WHITESPACE* ~ NEWLINE+) ~ phoneme_conditions)*}
phonetic_notations = @{phonetic_notation+}
phoneme_condition = { beginning_word | end_word | followed_by | between }

beginning_word = {^"at" ~ WHITESPACE+ ~ ^"the" ~ WHITESPACE+ ~ ^"beginning" ~ WHITESPACE+ ~ ^"of" ~ WHITESPACE+ ~ ^"word"}
end_word = {^"at" ~ WHITESPACE+ ~ ^"the" ~ WHITESPACE+ ~ ^"end" ~ WHITESPACE+ ~ ^"of" ~ WHITESPACE+ ~ ^"word"}

followed_by =  {^"followed" ~ WHITESPACE+ ~ ^"by" ~ WHITESPACE+ ~ "\"" ~ (class_name | letter+) ~ "\""}
between =  {^"between" ~ WHITESPACE+ ~ "\"" ~ (class_name | letter+) ~ "\"" ~ ^"and" ~ WHITESPACE+ ~ "\"" ~ (class_name | letter+) ~ "\""}

rules = { ^"rules" ~ ":" ~ NEWLINE* ~ rule ~ (NEWLINE+ ~ rule)* }

rule = { sound_rule | phoneme_rule }
sound_rule = { environment ~ ":" ~ input ~ "->" ~ output?  }
phoneme_rule = { input ~ "~>" ~ ((class_name | phonetic_notation)+)?  }

environment = ${ word_separator? ~ class_or_letter* ~ "_" ~ expand? ~ class_or_letter*  ~ word_separator? }
expand = { "*" }
word_separator = { "#" }
class_or_letter = _{ class_name | phonetic_notation }

input= @{ (!(NEWLINE | WHITESPACE | "->" | "#") ~ ANY)+ } 
output = @{ (!(NEWLINE | WHITESPACE)  ~ ANY)+ } 

NOT = { ^"not"}
AND = { ^"and" }
OR = { ^"or"}

WHITESPACE =  _{ " " | "\t" } 
COMMENT = _{ "%" ~ (!NEWLINE ~ ANY)* } 
